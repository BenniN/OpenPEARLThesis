/*
 [A "BSD license"]
 Copyright (c) 2012-2016 Rainer Mueller
 All rights reserved.

 Redistribution and use in source and binary forms, with or without
 modification, are permitted provided that the following conditions
 are met:

 1. Redistributions of source code must retain the above copyright
    notice, this list of conditions and the following disclaimer.
 2. Redistributions in binary form must reproduce the above copyright
    notice, this list of conditions and the following disclaimer in the
    documentation and/or other materials provided with the distribution.
 3. The name of the author may not be used to endorse or promote products
    derived from this software without specific prior written permission.

 THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

MODULE(sched_demo);
SYSTEM;
 termout: StdOut;
/*
__cpp__("pearlrt::StdOut ___termout;");
__cpp__("pearlrt::Device*  d_termout = &___termout;");
*/

PROBLEM;
SPC termout DATION OUT SYSTEM ALPHIC;

DCL console DATION OUT ALPHIC DIM(*,80) FORWARD /* GLOBAL */ CREATED(termout);

!SPC rr_test PROC GLOBAL;

TASK1: TASK PRIO 2 MAIN;

     DCL end CLOCK;
     DCL d DURATION INIT (10SEC);

     OPEN console;

     ACTIVATE TASK2 PRIO 10;

     PUT 'Task1...' TO console BY A, SKIP;

     end := NOW;
     end := end + d;   ! d = 10SEC 

     WHILE NOW < end REPEAT

        PUT NOW, 'Task 1 works' TO console BY T(15,6),X(3),A,SKIP;

        AFTER 2 SEC RESUME;

      END;

     PUT 'Task1: preventing Task3' TO console BY A,SKIP;

     PREVENT TASK3;

     PUT 'Task1: terminating Task2' TO console BY A,SKIP;
     TERMINATE TASK2;

    ! CALL rr_test;


     PUT 'Task1 finished' TO console BY A,SKIP;

      CLOSE console;

END;

TASK2: TASK PRIO 4;

   AFTER 5 SEC ALL 3 SEC ACTIVATE TASK3;

   REPEAT
      PUT NOW,'Task2 works' TO console BY T(15,6),X(15),A,SKIP;

      AFTER 5 SEC RESUME;

   END; ! of REPEAT
END;

TASK3: TASK PRIO 6;

   PUT NOW,'Task 3 works' TO console BY T(15,6),X(25),A,SKIP;

END;


MODEND;
