\section{LogFile}
Provides the capability to specify the log file name.
The LogFile needs another system device, which provides the access to a 
disc.

Synopsis: \texttt{LogFile(filename)}

\begin{description}
\item[filename]  is the name of the fil, which collects all log messages
   from the application.
\end{description}

\paragraph{Note:} The configuration element LogFile needs a disc type device
  the for the access to the phyical storage.
 

Sample Usage:
\begin{PEARLCode}
disc: Disc('/tmp/' ,10); ! linux specific disc definition
Log('EW') --- LogFile('pearl.log') --- disc;
\end{PEARLCode}

\section{I2c-Device: LM75}
The sensor delivers the current temperature via an I2C-bus interface.

Synopsis: \texttt{LM75(address)}

\begin{description}
\item[address] is the I2C adress of the sensor on the I2C bus.
   Valid adresses are: 48'B4, '49'B4, '4A'B4, '4B'B4,
            '4C'B4, '4D'B4 , '4E'B4, '4F'B4
\end{description}

\paragraph{Note:}\ 
\begin{itemize}
\item the temperatur is returned in thenth of degree centigrade as a 
    FIXED(15) value
\item The chip needs some time to update the temperature value. A gap of
   $\approx 40\mu s$ should be kept by the application between two read cycles.
\item The LM75 device needs an I2C-bus device to operate
\end{itemize}


Sample Usage:
\begin{PEARLCode}
...
SYSTEM;
   ! linux specific i2c bus interface definition
   ic2bus1: I2CBUS('/dev/i2c-0');

   ! connect the sensor to the I2C bus
   temp: LM75('48'B4) --- i2cbus1;
...
PROBLEM;
   SPC temperatur DATION IN SYSTEM BASIC FIXED(15);
   DCL uTemperatur DATION IN BASIC FIXED(15) CREATED(temperatur);
...
t1: TASK;
   DCL tValue FIXED(15);
...
   OPEN uTemperatur;
   TAKE tValue FROM uTemperatur;
   PUT tvalue/10,'.',tValue REM 10 TO console BY F(3),A,F(1), SKIP;
...
   CLOSE uTemperatur;
END;
...
\end{PEARLCode}

\section{I2c-Device: PCF8574}
This device acts as port expander with 8 bit numbered from 7
for the most significant bit down to 0.
The decice operates via an I2C bus. 
Individual bits may be ether as input or as output.
Input and output are treated in different system elements.

Synopsis: PCF8574In(address, start, width) \\
\ \ \ \ PCF8574Out(address, start, width)

The access of these devices must be done with BIT-types of the same size
as denoted by width.

\begin{description}
\item[address] is the I2C adress of the sensor on the I2C bus.
   Valid adresses are: '20'B4, '21'B4, '22'B4, '23'B4,
            '24'B4, '25'B4 , '26'B4, '27'B4,
            '38'B4, '39'B4, '3A'B4, '3B'B4, '3C'B4, '3D'B4 '3E'B4, '3F'B4
\item[start] is the starting bit number, starting at the most signifigant bit 
    number. 
\item[width] the number of consecutive bits to use
      towards the least significant direction
\end{description}

\paragraph{Notes:}\ 
\begin{itemize}
\item The PCF8574In device is read only
\item The PCF8574Out device is write only
\item Example: PCF8574In(5,3)  
\newline
\begin{tabular}{|c|c|c|c|c|c|c|c|l}

 \multicolumn{4}{l}{MSB}&\multicolumn{4}{r}{LSB}&\\
\cline{1-8}
 7 & 6 & 5 & 4 & 3 & 2 & 1 & 0 & bit numbers in datasheet\\
\cline{1-8}
% 1 & 2 & 3 & 4 & 5 & 6 & 7 & 8 & bit numbers in OpenPEARL\\
%\hline
   &   & x & x & x &   &   &   & PCF8574In(5,3) \\
\cline{1-8}
\end{tabular}

\end{itemize}

\begin{PEARLCode}
...
SYSTEM;
   ! linux specific i2c bus interface definition
   ic2bus1: I2CBUS('/dev/i2c-0');

   ! connect the led to bit 7 of the port expander
   ! via the I2C bus
   led: PCF8574Out('21'B4,7,1) --- i2cbus1;

   ! connect a switch to bit 6 of the port expander
   ! via the I2C bus
   switch: PCF8574In('21'B4,6,1) --- i2cbus1;

   ! connect a stepper motor to bit 3,2,1,0 of the port expander
   ! via the I2C bus
   stepper: PCF8574Out('21'B4,7,1) --- i2cbus1;

PROBLEM;
   SPC led DATION OUT SYSTEM BASIC BIT(1);
   DCL uLed DATION OUT BASIC BIT(1) CREATED (led);

   SPC switch DATION IN SYSTEM BASIC BIT(1);
   DCL uSwitch DATION IN BASIC BIT(1) CREATED (switch);

   SPC stepper DATION OUT SYSTEM BASIC BIT(4);
   DCL uStepper DATION OUT BASIC BIT(4) CREATED (stepper);

t1: TASK;
   DCL currentSetting BIT(1);

   OPEN uLed;
   OPEN uSwitch;
   OPEN uStepper;
   SEND '1'B1 TO uLed;  ! switch on
   SEND '0'B1 TO uLed;  ! switch off

   TAKE currentSetting FROM uSwitch;
   SEND currentSetting TO uLed;  ! echo the switch position

   SEND '1010'B1 TO uStepper;
 
   CLOSE uLed;
   CLOSE uSwitch;
   CLOSE uStepper;
...
END;
...
\end{PEARLCode}

\section{I2c-Device: PCA9685, PCA9685Channel}
This device acts as puls width modulation generator.
The PCA9685 contains 16 independent channels. The duty cycle
is specified by a 12 bit integer value (0..4095).

The device contains an internal 25MHz oscillator and an 8 bit
prescaler. Due to hardware restrictions, the prescaler values are
allowed from 3 to 255. This provides an update frequency between
$1.526 kHz$ and $24 Hz$.

The decice operates via an I2C bus. 

Synopsis: PCA9685Channel(channel) --- PCA9685(address, prescaler) --- i2cbus; \\

\begin{description}
\item[channel] denotes the channel of the PCA9685
\item[address] is the I2C adress of the sensor on the I2C bus.
   Valid adresses are: '40'B4 to '7F'B4
\item[prescaler] defines the dividor of the internal oscillator
   Valid values: 3 .. 255
\end{description}

\paragraph{Notes:}\ 
\begin{itemize}
\item The PCA9685 needs an I2C-device for operation
\item The outputs are configured as totem pole. This 
    setting may not set channel specific.
\item Check the requirements of your actor for maximum freqency
    and maximum duty cycle.
\end{itemize}

\begin{PEARLCode}
...
SYSTEM;
   ! linux specific i2c bus interface definition
   ic2bus1: I2CBUS('/dev/i2c-0');

   ! connect the PCA9685 to the the I2C bus
   ! default i2c-adress and default prescaler --> 200Hz
   pcaProvider: PCA9685('40'B4,30) --- i2cbus1;

   ! select channel 0 and 1 for led 0 and 1
   led0: PCA9685Channel(0) --- pcaProvider;
   led1: PCA9685Channel(1) --- pcaProvider;


PROBLEM;
   SPC led0 DATION OUT SYSTEM BASIC FIXED(15);
   DCL uLed0 DATION OUT BASIC FIXED(15) CREATED (led0);

   SPC led1 DATION OUT SYSTEM BASIC FIXED(15);
   DCL uLed1 DATION OUT BASIC FIXED(15) CREATED (led1);

t1: TASK;
   DCL half FIXED(15) INIT(2047);
   DCL nearlyOff FIXED(15) INIT(2);
   DCL fullOn FIXED(15) INIT(5000); ! larger than 4095

   OPEN uLed0;
   OPEN uLed1;
   SEND half   TO uLed0;  
   SEND fullOn TO uLed1;  

...
   CLOSE uLed0;
   CLOSE uLed1;
END;
...
\end{PEARLCode}

\section{I2c-Device: ADS1015SE}
The sensor provides four single ended analog inputs via an I2C bus.
The device  has a programmable gain unit, with modifies the sesibility
of the external voltage by a muliplication by a selecteable factor.

Synopsis: \texttt{ADS1015SE(address, channal, gain)}

The device returns a FIXED(15) value reflecting the external voltage as ratio
of the $\frac{returned value}{32767}*U_{ref}$.
The reference voltage $U_{ref}$ is usually the supply voltage of 3.3V or 5V.

\begin{description}
\item[address] is the I2C adress of the sensor on the I2C bus.
   Valid adresses are: '48'B4, '49'B4, '4A'B4, '4B'B4
\item[channel] is the number of the selected input channel. 
   Valid values are 0,1,2,3
\item[gain] is the selected gain.
   The result of the multiplication must not exceed the supply voltage.

  \begin{tabular}{l|l}
   gain & factor\\
   \hline 
   0 & 2/3 \\
   1 & 1 \\
   2 & 2 \\
   3 & 4 \\
   4 & 8 \\
   8 & 16 \\
   \end{tabular} 

\end{description}

\paragraph{Notes:} \ 
\begin{itemize}
\item The driver supports also the ADS1012 device.
\item The device is read only
\end{itemize}

Sample Usage:
\begin{PEARLCode}
...
SYSTEM;
   ! linux specific i2c bus interface definition
   ic2bus1: I2CBUS('/dev/i2c-0');

   ! connect the sensor to the I2C bus at 
   ! address 48 (hexadecimal)
   ! select channel 0 and internal gain of 1
   voltage1: ADS1015SE('48'B4,0,1) --- i2cbus1;

PROBLEM;
   SPC voltage1 DATION IN SYSTEM BASIC FIXED(15);
   DCL u1 DATION IN BASIC FIXED(15) CREATED(voltage1);

t1: TASK;
   DCL uFixed FIXED(15);
   DCL u FLOAT;
 
   OPEN u1;
   TAKE uFixed FROM u1;
   u := TOFLOAT(uFixed)/32767.0 * 3.3;
   PUT 'voltage:', u, 'V' TO console BY A,X,F(5,3),A,SKIP;
   CLOSE u1;
END;
...
\end{PEARLCode}


\section{I2c-Device: BME280}
The sensor delivers the current temperature, pressure and humidity
 via an I2C-bus interface.

Synopsis: \texttt{BME260(address,tempOS, pressureOS, humOS,iir, tSleep)}

\begin{description}
\item[address] is the I2C adress of the sensor on the I2C bus.
   Valid adresses are: 78'B4, '79'B4
\item[tempOS] oversampling for the temperature measurement
\item[pressureOS] oversampling for the pressure measurement
\item[humOS] oversampling for the humidity measurement
\item[iir] filter for temperature and pressure measurements
\item [tSleep] if non zero, automatic sleep time between
 continuous measurements. 
If zero, the conversion starts at each TAKE  statement
 in \textit{forced mode}. The sensor needs sone miliseconds depending
on oversampling and channel selections between measure
 request and available data. This time is calculated from the
  datasheet and inserted before data reading.
\end{description}
For each oversampling one of the following values are allowed:

\begin{tabular}{|l|l|}
\hline
xOS & meaning \\
\hline
0 & measurement switched off\\
1 & no oversampling \\
2 & average over 2 measurements \\
3 & average over 4 measurements \\
4 & average over 8 measurements \\
5 & average over 16 measurements \\
\hline
\end{tabular}

The iir-filter (infinite impulse response filter) may be configured as:

\begin{tabular}{|l|l|}
\hline
iir & meaning \\
\hline
0 & no filter \\
1 & 2 measurements \\
2 & 4 measurements \\
3 & 8 measurements \\
4 & 16 measurements \\
\hline
\end{tabular}

For tSleep, the following values are supported:

\begin{tabular}{|l|l|}
\hline
tSleep & meaning \\
\hline
0 & forced mode \\
1 & 0.5 ms \\
2 & 62.5 ms \\
3 & 125 ms \\
4 & 250 ms \\
5 & 500 ms \\
6 & 1000 ms \\
7 & 10 ms \\
8 & 20 ms \\
\hline
\end{tabular}

The result are passed in a data structure with three \texttt{FIXED(31)} values.
This may be an array od a struct like:
\paragraph{Note:}\ 
\begin{itemize}
\item the temperatur is returned in hunderdth of degree centigrade as a 
    FIXED(31) value
\item the pressure is passed an tenth of Pascal 
\item the humidity is passed as hunderth of percent
\end{itemize}

The manufacturer Bosch recommends the following setting according 
the application:
\begin{description}
\item[wether monitoring]
   \begin{itemize}
   \item forced mode, 1 measurement per minute
   \item oversampling: temperature x1, pressure x1, humidity x1 \newline
     no oversampling at all
   \item iir: off
   \item \texttt{BME280('76'B4, 1,1,1,0,0);}
   \end{itemize}
\item[Humidity Sensing]
   \begin{itemize}
   \item forced mode, 1 measurement per second
   \item oversampling: temperature x1, pressure x0, humidity x1 \newline
     no oversampling at all
   \item iir: off
   \item \texttt{BME280('76'B4, 1,0,1,0,0);}
   \end{itemize}
\item[Indoor Navigation]
   \begin{itemize}
   \item normal mode, tSleep = 0,5ms
   \item oversampling: temperature x2, pressure x16, humidity x1
   \item iir: 16
   \item \texttt{BME280('76'B4, 2,5,1,4,1);}
   \item Data rate: 25 Hz
   \end{itemize}
\item[Gaming]
   \begin{itemize}
   \item normal mode, tSleep = 0,5ms
   \item oversampling: temperature x1, pressure x4, humidity x0
   \item iir: 16
   \item \texttt{BME280('76'B4, 1,3,0,4,1);}
   \item Data rate: 83 Hz
   \end{itemize}

\end{description}


Sample Usage:
\begin{PEARLCode}
...
SYSTEM;
   ! linux specific i2c bus interface definition
   ic2bus1: I2CBUS('/dev/i2c-0');

   ! connect the sensor to the I2C bus
   bme: BME280('76'B4,1,2,3,4,5) --- i2cbus1;
...
PROBLEM;
   TYPE STRUCT [ temp     FIXED(31), 
                 pressure FIXED(31),
                 hmidity  FIXED(31) ] BmeData;
   SPC bme DATION IN SYSTEM BASIC BmeData;
   DCL bme280 DATION IN BASIC BmeData CREATED(bme);
...
t1: TASK;
   DCL bmeData BmeData;
...
   OPEN bme280;
   TAKE bmeData FROM uTemperatur;
   PUT bmeData.temp/100,' degree C ',
       bmeData.pressure/100,' Pa' ,
       bmeData.humidity/100 ,'%'
    TO console BY F(3),A,F(7),A,F(3),A, SKIP;
...
   CLOSE bme280;
END;
...
\end{PEARLCode}

